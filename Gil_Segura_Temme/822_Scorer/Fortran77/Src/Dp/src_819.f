        SUBROUTINE GIZ(IFACG,X,Y,REG,IMG,REGP,IMGP,IERROG)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC               
CCCC CALCULATION OF GI(Z). WE USE:                       
CCCC      1) SERIES,   |Z|<1.5
CCCC      2) ASYMPTOTIC EXPANSION, |Z|>30 AND PHASE(Z)<PI/3.
CCCC      3) CONNECTION FORMULAE. LET PH=PHASE(Z), 
CCCC         3.A)  GI(Z)=BI(Z)-HI(Z),   2PI/3<|PH|<PI
CCCC         3.B)  GI(Z)=-EXP(2*I*PI/3)*HI(Z*EXP(2*I*PI/3))+
CCCC                      I*AI(Z),    0<|PH|<2PI/3
CCCC 
CCCC CALCULATION OF GI'(Z). WE USE:
CCCC      1) SERIES,   |Z|<1.5
CCCC      2) ASYMPTOTIC EXPANSION, |Z|>30 AND PHASE(Z)<PI/3. 
CCCC      3) CONNECTION FORMULAE.
CCCC         3.A)  GI'(Z)=BI'(Z)-HI'(Z),  2PI/3<|PH|<PI
CCCC         3.B)  GI'(Z)=-EXP(4*PI*I/3)*HI'(Z*EXP(2*I*PI/3))+
CCCC                       I*AI'(Z),    0<|PH|<2PI/3
CCCC IFACG:
CCCC     * IFACG=1, THE CODE COMPUTES GI(Z) AND GI'(Z) IN 
CCCC                THE COMPLEX PLANE
CCCC     * IFACG=2, THE CODE COMPUTES:
CCCC                 A) NORMALIZED GI(Z) AND GI'(Z), PI/3<|PH|<PI
CCCC                 B) GI(Z) AND GI'(Z), 0<|PH|<PI/3
CCCC IERROG: ERROR FLAG
CCCC     * IF IERROG=0, COMPUTATION SUCCESSFUL.
CCCC     * IF IERROG=1, COMPUTATION OUT OF RANGE.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 
CCCC     MACHINE DEPENDENT CONSTANTS: FUNCTION D1MACH
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCCC  ACCURACY:
CCCC  THE RELATIVE ACCURACY FOR THE MODULUS OF THE FUNCTIONS IS
CCCC  BETTER THAN 1.D-12, EXCEPT NEAR THE ZEROS OF THE FUNCTION,
CCCC  WHERE THE ACCURACY MUST BE INTERPRETED AS ABSOLUTE. 
CCCC  REGARDING THE COMPUTATION OF THE PHASE OF THE 
CCCC  FUNCTIONS, 1.D-12 IS THE ABSOLUTE ACCURACY.   
CCCC  THE ACCURACY OF THE CODES IS LIMITED BY THE ACCURACY IN THE
CCCC  COMPUTATION OF THE AIRY FUNCTIONS AI(Z) AND BI(Z) IN THE 
CCCC  SECTORS OF THE COMPLEX PLANE WHERE CONNECTION FORMULAS ARE
CCCC  USED. THEREFORE, THE ACCURACY IN THE COMPUTATION OF THE
CCCC  UNSCALED AIRY FUNCTIONS HI(Z) AND GI(Z) WILL GRADUALLY 
CCCC  WORSEN AS LARGER |Z| VALUES (|Z|>30) ARE CONSIDERED. 
CCCC  THIS DEGRADATION IN ACCURACY IS ELIMINATED IN THE SECTORS
CCCC  WHERE SCALING IS POSSIBLE.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCCC                LIST OF VARIABLES 
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCCC AII   :  IMAGINARY PART OF AI(Z)
CCCC AIR   :  REAL PART OF AI(Z)
CCCC BII   :  IMAGINARY PART OF BI(Z)
CCCC BIR   :  REAL PART OF BI(Z)
CCCC C     :  COS(2*PI/3)
CCCC C1    :  COS(3*PHASE(Z)/2)
CCCC C11   :  COS( 2/3*|Z|**(3/2)*SIN(3*PHASE(Z)/2) )
CCCC COVER :  2/3*|Z|**(3/2)*|COS(PHASE(Z)*3/2)|
CCCC D1MACH:  PROVIDES THE MACHINE DEPENDENT CONSTANTS
CCCC DEX   :  EXP(- 2/3*|Z|**(3/2)*COS(3*PHASE(Z)/2) )
CCCC DF1   :  2/3*|Z|**(3/2)*COS(3*PHASE(Z)/2)
CCCC DF2   :  2/3*|Z|**(3/2)*SIN(3*PHASE(Z)/2)
CCCC DIMA  :  -EXP(- 2/3*|Z|**(3/2)*COS(3*PHASE(Z)/2) )*
CCCC            SIN( 2/3*|Z|**(3/2)*SIN(3*PHASE(Z)/2) )
CCCC DL1   :  LOGARITHM OF THE OVERFLOW NUMBER
CCCC DL2   :  - LOGARITHM OF THE UNDERFLOW NUMBER
CCCC DRE   :  EXP(- 2/3*|Z|**(3/2)*COS(3*PHASE(Z)/2) )*
CCCC            COS( 2/3*|Z|**(3/2)*SIN(3*PHASE(Z)/2) )
CCCC F23   :  2/3
CCCC F23R  :  2/3*|Z|**(3/2)
CCCC IERRO :  ERROR FLAG FOR THE COMPUTATION OF AI(Z) OR BI(Z)
CCCC IERROG:  ERROR FLAG FOR THE COMPUTATION OF GI(Z)
CCCC IERROH:  ERROR FLAG FOR THE COMPUTATION OF HI(Z)
CCCC IEXPF :  OVERFLOW PARAMETER
CCCC IFAC  :  PARAMETER FOR THE COMPUTATION OF THE HOMOGENEOUS AIRY  
CCCC          FUNCTIONS:
CCCC          * IF IFAC=1, COMPUTATION OF UNSCALED AIRY FUNCTIONS.
CCCC          * IF IFAC=2, COMPUTATION OF SCALED AIRY FUNCTIONS.       
CCCC IFACG :
CCCC          * IF IFACG=1, COMPUTATION OF UNSCALED GI(Z).
CCCC          * IF IFACG=2, COMPUTATION OF SCALED GI(Z).
CCCC IFACGS:  STORES THE VALUE OF IFACG USED AS INPUT 
CCCC IFACH :  
CCCC          * IF IFACH=1, COMPUTATION OF UNSCALED HI(Z).
CCCC          * IF IFACH=2, COMPUTATION OF SCALED HI(Z).  
CCCC IFUN  :  PARAMETER FOR THE COMPUTATION OF THE HOMOGENEOUS AIRY
CCCC          FUNCTIONS:
CCCC          * IF IFUN=1, COMPUTATION OF AI(Z) OR BI(Z).
CCCC          * IF IFUN=2, COMPUTATION OF AI'(Z) OR BI'(Z).
CCCC IMG   :  IMAGINARY PART OF THE FUNCTION GI(Z)
CCCC IMG1  :  CONTRIBUTION TO THE IMAGINARY PART OF GI(Z)
CCCC IMGP  :  IMAGINARY PART OF THE FUNCTION GI'(Z)
CCCC IMGP1 :  CONTRIBUTION TO THE IMAGINARY PART OF GI'(Z)
CCCC IMH   :  IMAGINARY PART OF -EXP(2/3*Z**(3/2))*EXP(2*PI*I/3)*
CCCC                         HI(Z*EXP(2*PI*I/3))
CCCC IMH1  :  IMAGINARY PART OF HI(Z*EXP(2*PI*I/3))
CCCC IMH2  :  IMAGINARY PART OF -EXP(2*PI*I/3)*HI(Z*EXP(2*PI*I/3))
CCCC IMHP  :  IMAGINARY PART OF -EXP(2/3*Z**(3/2))*EXP(2*PI*I/3)*
CCCC                         HI'(Z*EXP(2*PI*I/3))
CCCC IMHP1 :  IMAGINARY PART OF HI'(Z*EXP(2*PI*I/3))
CCCC IMHP2 :  IMAGINARY PART OF -EXP(2*PI*I/3)*HI'(Z*EXP(2*PI*I/3))
CCCC IZ    :  FLAG FOR THE PARTICULAR CASE Z=0 (IZ=1) 	      
CCCC OVER  :  OVERFLOW NUMBER
CCCC PH    :  PHASE OF Z
CCCC PHASE :  FUNCTION FOR THE CALCULATION OF THE PHASE OF 
CCCC          A COMPLEX NUMBER
CCCC PI    :  3.1415...
CCCC PI13  :  PI/3
CCCC PI23  :  2*PI/3
CCCC PIHAL :  PI/2
CCCC R     :  |Z|
CCCC R32   :  |Z|**(3/2)
CCCC REG   :  REAL PART OF THE FUNCTION GI(Z)
CCCC REG1  :  CONTRIBUTION TO THE REAL PART OF GI(Z)
CCCC REGP  :  REAL PART OF THE FUNCTION GI'(Z)
CCCC REGP1 :  CONTRIBUTION TO THE REAL PART OF GI'(Z)
CCCC REH   :  REAL PART OF -EXP(2/3*Z**(3/2))*EXP(2*PI*I/3)*
CCCC                         HI(Z*EXP(2*PI*I/3))
CCCC REH1  :  REAL PART OF HI(Z*EXP(2*PI*I/3))
CCCC REH2  :  REAL PART OF -EXP(2*PI*I/3)*HI(Z*EXP(2*PI*I/3))
CCCC REHP  :  REAL PART OF -EXP(2/3*Z**(3/2))*EXP(2*PI*I/3)*
CCCC                         HI'(Z*EXP(2*PI*I/3))
CCCC REHP1 :  REAL PART OF HI'(Z*EXP(2*PI*I/3))
CCCC REHP2 :  REAL PART OF -EXP(2*PI*I/3)*HI'(Z*EXP(2*PI*I/3))
CCCC S     :  SIN(2*PI/3)
CCCC S1    :  SIN(3*PHASE(Z)/2)
CCCC S11   :  SIN( 2/3*|Z|**(3/2)*SIN(3*PHASE(Z)/2) )
CCCC SQRT3 :  3**(1/2)
CCCC TH15  :  PHASE(Z)*3/2
CCCC U     :  REAL PART OF Z*EXP(2*PI*I/3)
CCCC UNDER :  UNDERFLOW NUMBER
CCCC V     :  IMAGINARY PART OF  Z*EXP(2*PI*I/3)
CCCC Y     :  IMAGINARY PART OF Z
CCCC YA    :  IMAGINARY PART OF Z
CCCC X     :  REAL PART OF Z
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     AUTHORS:                                               
C        AMPARO GIL    (U. AUTONOMA DE MADRID, MADRID, SPAIN). 
C                      E-MAIL: AMPARO.GIL@UAM.ES
C        JAVIER SEGURA (U. CARLOS III DE MADRID, MADRID, SPAIN).
C                      E-MAIL: JSEGURA@MATH.UC3M.ES
C        NICO M. TEMME (CWI, AMSTERDAM, THE NETHERLANDS).
C                      E-MAIL: NICO.TEMME@CWI.NL
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C    REFERENCES:                                                
C         ON NON-OSCILLATING INTEGRALS FOR COMPUTING 
C         INHOMOGENEOUS AIRY FUNCTIONS.
C         A. GIL, J. SEGURA, N.M. TEMME
C         MATH. COMPUT. 70 (2001) 1183-1194.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      DOUBLE PRECISION X,Y,REG,IMG,REGP,IMGP
      DOUBLE PRECISION YA,REH,IMH,REHP,IMHP
      DOUBLE PRECISION PI,PIHAL,F23,PI13,PI23,SQRT3,R,R32,PHASE,PH,
     +COVER,OVER,UNDER,DL1,DL2,D1MACH,C,S,U,V
      DOUBLE PRECISION TH15,S1,C1,F23R,DF1,DF2,S11,C11,DEX,DRE,DIMA
      DOUBLE PRECISION AIR,AII,BIR,BII,REH1,IMH1,REHP1,IMHP1,
     +REH2,IMH2,REHP2,IMHP2,REG1,IMG1,REGP1,IMGP1
      INTEGER N,IFUN,IFAC,IEXPF,IFACH,IFACG,IFACGS,IERRO,IERROH,
     +IERROG,IZ
      COMMON/PARAM1/PI,PIHAL
      YA=Y
      IFACGS=IFACG
      IF (YA.LT.0.D0) THEN
        Y=-Y
      ENDIF
      PI=ACOS(-1.D0)
      PIHAL=PI*0.5D0
      PI23=2.D0*PI/3.D0
      PI13=PI/3.D0
      F23=.6666666666666667D0
      SQRT3=1.7320508075688772935D0
      R=SQRT(X*X+Y*Y)
      R32=R*SQRT(R)
      PH=PHASE(X,Y)
      IERROG=0
      IFAC=1
      IFACH=1
      IEXPF=0
      COVER=F23*R32*ABS(COS(1.5D0*PH))
CCC MACHINE DEPENDENT CONSTANT (OVERFLOW NUMBER)                             
      OVER=D1MACH(2)*1.D-3
CCC MACHINE DEPENDENT CONSTANT (UNDERFLOW NUMBER)    
      UNDER=D1MACH(1)*1.D+3 
      DL1=LOG(OVER)
      DL2=-LOG(UNDER)
      IF (DL1.GT.DL2) OVER=1/UNDER 
      IF (IFACG.EQ.2) THEN      
CCC IS Z INSIDE THE SECTOR FOR NORMALIZED FUNCTIONS?
CCC   IF NOT, NON NORMALIZED FUNCTIONS ARE COMPUTED 
        IF (PH.LT.PI13) THEN
          IFACG=1
        ENDIF
      ENDIF
      IF (IFACG.EQ.1) THEN                 
        IF (PH.GE.PI13) THEN                                 
          IF (COVER.GE.LOG(OVER)) THEN
CCC OVERFLOW/UNDERFLOW PROBLEMS. 
CCC   CALCULATION ABORTED
            IERROG=1
            REG=0
            IMG=0
            REGP=0
            IMGP=0
          ENDIF
        ENDIF
      ELSE
        IF (COVER.GE.(LOG(OVER)*0.2)) THEN
          IEXPF=1
          IFAC=2  
        ENDIF
      ENDIF
      IZ=0
CCC  PARTICULAR CASE: Z=0
      IF ((ABS(X).LE.UNDER).AND.(ABS(Y).LE.UNDER)) THEN
        REG=.2049755424820002D0
        IMG=0.D0
        REGP=.149429452451275D0
        IMGP=0.D0
        IZ=1
      ENDIF
      IF ((IERROG.EQ.0).AND.(IZ.EQ.0)) THEN
        IF (R.LT.1.5) THEN
CCC USE OF SERIES
          CALL GIZSER(X,Y,REG1,IMG1,REGP1,IMGP1)
          IF ((IFACG.EQ.2).AND.(PH.GE.PI13)) THEN
            TH15=1.5D0*PH
            S1=SIN(TH15)
            C1=COS(TH15)
            F23R=F23*R32
            DF1=F23R*C1
            DF2=F23R*S1
            S11=SIN(DF2)
            C11=COS(DF2)
            DEX=EXP(DF1)
            DRE=DEX*C11
            DIMA=DEX*S11
            REG=(DRE*REG1-DIMA*IMG1)/PI
            IMG=(DRE*IMG1+DIMA*REG1)/PI
            REGP=(DRE*REGP1-DIMA*IMGP1)/PI
            IMGP=(DRE*IMGP1+DIMA*REGP1)/PI
          ELSE
            REG=REG1/PI
            IMG=IMG1/PI
            REGP=REGP1/PI
            IMGP=IMGP1/PI
          ENDIF
        ELSE
          IF ((R.GT.30).AND.(PH.LT.(PI13-0.3D0))) THEN          
CCC USE OF ASYMPTOTIC EXPANSION                            
            CALL HIZEXP(X,Y,REG,IMG,REGP,IMGP)                
            REG=REG/PI             
            IMG=IMG/PI
            REGP=-REGP/PI
            IMGP=-IMGP/PI
          ELSE
CCCCCCCCC CONNECTION FORMULAE
            IF (ABS(PH).LT.PI23) THEN
CCCCCC  CONNECTION FORMULA 1:
CCC     N= 1: TRANSFORM Z TO W= U+IV=Z EXP( 2 PI I/3)
              IF (IEXPF.EQ.0) THEN
                N=1
                C=-0.5D0
                S=N*0.5*SQRT3
                U=X*C-Y*S
                V=X*S+Y*C
                CALL HIZ(IFACH,U,V,REH1,IMH1,REHP1,IMHP1,IERROH)
                REH2=-(C*REH1-S*IMH1)
                IMH2=-(S*REH1+C*IMH1)
                S=-S
                REHP2=-(C*REHP1-S*IMHP1)
                IMHP2=-(S*REHP1+C*IMHP1)
                IF (IFACG.EQ.2) THEN
                  TH15=1.5D0*PH
                  S1=SIN(TH15)
                  C1=COS(TH15)
                  F23R=F23*R32
                  DF1=F23R*C1
                  DF2=F23R*S1
                  S11=SIN(DF2)
                  C11=COS(DF2)
                  DEX=EXP(DF1)
                  DRE=DEX*C11
                  DIMA=DEX*S11
                  REH=(DRE*REH2-DIMA*IMH2)
                  IMH=(DRE*IMH2+DIMA*REH2)
                  REHP=(DRE*REHP2-DIMA*IMHP2)
                  IMHP=(DRE*IMHP2+DIMA*REHP2)
                  IFAC=2
                ELSE
                  REH=REH2
                  IMH=IMH2
                  REHP=REHP2
                  IMHP=IMHP2
                ENDIF
              ELSE
                REH=0
                IMH=0
                REHP=0
                IMHP=0
                IFAC=2  
              ENDIF
              IFUN=1
              CALL AIZ(IFUN,IFAC,X,Y,AIR,AII,IERRO)
              REG=REH-AII
              IMG=IMH+AIR
              IFUN=2
              CALL AIZ(IFUN,IFAC,X,Y,AIR,AII,IERRO)
              REGP=REHP-AII
              IMGP=IMHP+AIR
            ELSE
CCCCCC  CONNECTION FORMULA 2:
              IF (IEXPF.EQ.0) THEN
                CALL HIZ(IFACH,X,Y,REH1,IMH1,REHP1,IMHP1,IERROH)
                IF (IFACG.EQ.2) THEN
                  TH15=1.5D0*PH
                  S1=SIN(TH15)
                  C1=COS(TH15)
                  F23R=F23*R32
                  DF1=F23R*C1
                  DF2=F23R*S1
                  S11=SIN(DF2)
                  C11=COS(DF2)
                  DEX=EXP(DF1)
                  DRE=DEX*C11
                  DIMA=DEX*S11
                  REH=(DRE*REH1-DIMA*IMH1)
                  IMH=(DRE*IMH1+DIMA*REH1)
                  REHP=(DRE*REHP1-DIMA*IMHP1)
                  IMHP=(DRE*IMHP1+DIMA*REHP1)
                  IFAC=2
                ELSE
                  REH=REH1
                  IMH=IMH1
                  REHP=REHP1
                  IMHP=IMHP1
                ENDIF
              ELSE
                REH=0
                IMH=0
                REHP=0
                IMHP=0
                IFAC=2 
              ENDIF
              IFUN=1
              CALL BIZ(IFUN,IFAC,X,Y,BIR,BII,IERRO)
              REG=BIR-REH
              IMG=BII-IMH
              IFUN=2
              CALL BIZ(IFUN,IFAC,X,Y,BIR,BII,IERRO)
              REGP=BIR-REHP
              IMGP=BII-IMHP
            ENDIF
          ENDIF
        ENDIF
      ENDIF
      IF (YA.LT.0.D0) THEN
        Y=YA
        IMG=-IMG
        IMGP=-IMGP
      ENDIF
      IF ((YA.EQ.0.D0).AND.(IFACG.EQ.1)) THEN
        IMG=0.D0
        IMGP=0.D0
      ENDIF
      IFACG=IFACGS
      RETURN
      END
      SUBROUTINE GIZSER(X,Y,REG,IMG,REGP,IMGP)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC   GI(Z), GI'(Z)  SERIES, COMPLEX Z     CCC
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC         LIST OF VARIABLES 
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC CK     : SUCCESIVE COEFFICIENTS OF THE SERIES 
CCC CO     : ARRAY OF THE COEFFICIENTS OF THE 
CCC          SERIES
CCC D1MACH : FUNCTION FOR THE COMPUTATION OF
CCC          MACHINE DEPENDENT CONSTANTS
CCC DELTAI : CONTRIBUTION TO THE IMAGINARY PART OF
CCC          THE SERIES FOR GI(Z)
CCC DELTAR : CONTRIBUTION TO THE REAL PART OF THE
CCC          SERIES FOR GI(Z)
CCC DELTIP : CONTRIBUTION TO THE IMAGINARY PART OF 
CCC          THE SERIES FOR GI'(Z)
CCC DELTRP : CONTRIBUTION TO THE REAL PART OF THE
CCC          SERIES FOR GI'(Z)
CCC DFI    : ELEMENTARY CONTRIBUTION TO THE 
CCC          IMAGINARY PART OF THE SERIES FOR GI(Z)
CCC          AND GI'(Z)
CCC DFII   : ELEMENTARY CONTRIBUTION TO THE 
CCC          IMAGINARY PART OF THE SERIES FOR GI(Z)
CCC          AND GI'(Z)
CCC DFR    : ELEMENTARY CONTRIBUTION TO THE REAL
CCC          PART OF THE SERIES FOR GI(Z) AND GI'(Z)
CCC DFRR   : ELEMENTARY CONTRIBUTION TO THE REAL
CCC          PART OF THE SERIES FOR GI(Z) AND GI'(Z)
CCC EPS    : PRECISION
CCC IMG    : IMAGINARY PART OF GI(Z)
CCC IMGP   : IMAGINARY PART OF GI'(Z)
CCC R2     : |Z|**2 
CCC REG    : REAL PART OF GI(Z)
CCC REGP   : REAL PART OF GI'(Z)
CCC SUMAI  : ACCUMULATES THE CONTRIBUTION TO THE IMAGINARY 
CCC          PART OF GI(Z)
CCC SUMAIP : ACCUMULATES THE CONTRIBUTION TO THE IMAGINARY 
CCC          PART OF GI'(Z)
CCC SUMAR  : ACCUMULATES THE CONTRIBUTION TO THE REAL 
CCC          PART OF GI(Z)
CCC SUMARP : ACCUMULATES THE CONTRIBUTION TO THE REAL 
CCC          PART OF GI'(Z)
CCC Y      : REAL PART OF Z
CCC X      : IMAGINARY PART OF Z
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      DOUBLE PRECISION X,Y,EPS,D1MACH,REG,IMG,REGP,IMGP
      DOUBLE PRECISION CK,DFRR,DFII,SUMAR,SUMAI,DFR,DFI,DELTAR,
     +DELTAI
      DOUBLE PRECISION R2,SUMARP,SUMAIP,DELTRP,DELTIP
      DOUBLE PRECISION CO(0:40)
      INTEGER K
      SAVE CO
      DATA CO(0)/.64394965842703454362D0/
      DATA CO(1)/.46944647005087228316D0/
      DATA CO(2)/-.50000000000000000000D0/
      DATA CO(3)/.10732494307117242394D0/
      DATA CO(4)/ .39120539170906023596D-1/
      DATA CO(5)/-.25000000000000000000D-1/
      DATA CO(6)/.35774981023724141313D-2/
      DATA CO(7)/.93144140883109579990D-3/
      DATA CO(8)/-.44642857142857142857D-3/
      DATA CO(9)/.49687473644061307378D-4/
      DATA CO(10)/.10349348987012175554D-4/
      DATA CO(11)/-.40584415584415584416D-5/
      DATA CO(12)/.37642025487925232862D-6/
      DATA CO(13)/.66341980685975484324D-7/
      DATA CO(14)/-.22299129441986584844D-7/
      DATA CO(15)/.17924774041869158506D-8/
      DATA CO(16)/.27642491952489785135D-9/
      DATA CO(17)/-.81982093536715385455D-10/
      DATA CO(18)/.58577692947284831717D-11/
      DATA CO(19)/.80825999861081243083D-12/
      DATA CO(20)/-.21574235141240890909D-12/
      DATA CO(21)/.13947069749353531362D-13/
      DATA CO(22)/.17494805164736199802D-14/
      DATA CO(23)/-.42636828342373302192D-15/
      DATA CO(24)/.25266430705350600292D-16/
      DATA CO(25)/.29158008607893666335D-17/
      DATA CO(26)/-.65595120526728157219D-18/
      DATA CO(27)/.35992066531838461956D-19/
      DATA CO(28)/.38568794454885802032D-20/
      DATA CO(29)/-.80782168136364725639D-21/
      DATA CO(30)/.41370191415906278110D-22/
      DATA CO(31)/.41471821994500862401D-23/
      DATA CO(32)/-.81433637234238634716D-24/
      DATA CO(33)/.39176317628699126998D-25/
      DATA CO(34)/.36962408194742301605D-26/
      DATA CO(35)/-.68431627927931625812D-27/
      DATA CO(36)/.31092315578332640475D-28/
      DATA CO(37)/.27749555701758484688D-29/
      DATA CO(38)/-.48671143618728041118D-30/
      DATA CO(39)/.20979970025865479402D-31/
      DATA CO(40)/.17788176731896464543D-32/
      EPS=D1MACH(3)
      IF (EPS.LT.1.D-15) EPS=1.D-15      
      R2=X*X+Y*Y
      K=0
      CK=CO(0)
      DFRR=1.D0
      DFII=0.D0
      SUMAR=CK
      SUMAI=0.D0
      SUMARP=0.D0
      SUMAIP=0.D0
33    CK=CO(K+1)
      DFR=DFRR
      DFI=DFII 
      DFRR=DFR*X-DFI*Y
      DFII=DFR*Y+DFI*X
      DELTAR=DFRR*CK
      DELTAI=DFII*CK
      DELTRP=DELTAR*(K+1)
      DELTIP=DELTAI*(K+1)
      SUMAR=SUMAR+DELTAR
      SUMAI=SUMAI+DELTAI
      SUMARP=SUMARP+DELTRP
      SUMAIP=SUMAIP+DELTIP
      K=K+1
      IF (SUMAR.NE.0) THEN
        IF (ABS(DELTAR/SUMAR).GT.EPS)  GOTO 33
      ENDIF
      IF (SUMAI.NE.0) THEN
        IF (ABS(DELTAI/SUMAI).GT.EPS) GOTO 33
      ENDIF 
      IF (SUMARP.NE.0) THEN
        IF (ABS(DELTRP/SUMARP).GT.EPS)  GOTO 33
      ENDIF
      IF (SUMAIP.NE.0) THEN
        IF (ABS(DELTIP/SUMAIP).GT.EPS) GOTO 33
      ENDIF 
      REG=SUMAR
      IMG=SUMAI
      REGP=(X*SUMARP+Y*SUMAIP)/R2
      IMGP=-(Y*SUMARP-X*SUMAIP)/R2
      RETURN
      END
      SUBROUTINE HIZ(IFACH,X,Y,REH,IMH,REHP,IMHP,IERROH)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCCC CALCULATION OF HI(Z) AND HI'(Z). WE USE:
CCCC      1) SERIES,   |Z|<1.5
CCCC      2) LET PH=PHASE(Z)
CCCC         2.A)  NUMERICAL QUADRATURE,   2PI/3<|PH|<PI¡
CCCC         2.B)  CONNECTION FORMULAE,        0<|PH|<2PI/3
CCCC      3) ASYMPTOTIC EXPANSIONS,  |Z|>20
CCCC IFACH:
CCCC     * IFACH=1, THE CODE COMPUTES HI(Z) AND HI'(Z) IN 
CCCC                THE COMPLEX PLANE
CCCC     * IFACH=2, THE CODE COMPUTES:
CCCC                 A) NORMALIZED HI(Z) AND HI'(Z), |PH|<PI/3
CCCC                 B) HI(Z) AND HI'(Z), PI/3<|PH|<PI
CCCC IERROH: ERROR FLAG
CCCC     * IF IERROH=0, COMPUTATION SUCCESSFUL.
CCCC     * IF IERROH=1, COMPUTATION OUT OF RANGE.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCCC     MACHINE DEPENDENT CONSTANTS: FUNCTION D1MACH
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCCC  ACCURACY: 
CCCC  THE RELATIVE ACCURACY FOR THE MODULUS OF THE FUNCTIONS IS
CCCC  BETTER THAN 1.D-12, EXCEPT NEAR THE ZEROS OF THE FUNCTION,
CCCC  WHERE THE ACCURACY MUST BE INTERPRETED AS ABSOLUTE. 
CCCC  REGARDING THE COMPUTATION OF THE PHASE OF THE 
CCCC  FUNCTIONS, 1.D-12 IS THE ABSOLUTE ACCURACY.   
CCCC  THE ACCURACY OF THE CODES IS LIMITED BY THE ACCURACY IN THE
CCCC  COMPUTATION OF THE AIRY FUNCTIONS AI(Z) AND BI(Z) IN THE 
CCCC  SECTORS OF THE COMPLEX PLANE WHERE CONNECTION FORMULAS ARE
CCCC  USED. THEREFORE, THE ACCURACY IN THE COMPUTATION OF THE
CCCC  UNSCALED AIRY FUNCTIONS HI(Z) AND GI(Z) WILL GRADUALLY 
CCCC  WORSEN AS LARGER |Z| VALUES (|Z|>30) ARE CONSIDERED. 
CCCC  THIS DEGRADATION IN ACCURACY IS ELIMINATED IN THE SECTORS                                     
CCCC  WHERE SCALING IS POSSIBLE.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCCC                  LIST OF VARIABLES 
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCCC AII   :  IMAGINARY PART OF EXP(-PI*I/6)*AI(Z*EXP(-2*PI*I/3))               	                
CCCC AII1  :  IMAGINARY PART OF AI(Z*EXP(-2*PI*I/3))                         		
CCCC AIR   :  REAL PART OF EXP(-PI*I/6)*AI(Z*EXP(-2*PI*I/3))                       				
CCCC AIR1  :  REAL PART OF AI(Z*EXP(-2*PI*I/3))                              				
CCCC C     :  COS(2*PI/3)                                                  				
CCCC C1    :  COS(3*PHASE(Z)/2)	                                         			
CCCC C11   :  COS( 2/3*|Z|**(3/2)*SIN(3*PHASE(Z)/2) )                           				
CCCC CC    :  COS(PI/6)								
CCCC COVER :  2/3*|Z|**(3/2)*|COS(PHASE(Z)*3/2)|				
CCCC DEX   :  EXP(- 2/3*|Z|**(3/2)*COS(3*PHASE(Z)/2) )				
CCCC DF1   :  2/3*|Z|**(3/2)*COS(3*PHASE(Z)/2)				
CCCC DF2   :  2/3*|Z|**(3/2)*SIN(3*PHASE(Z)/2)				
CCCC DIMA  : -EXP(- 2/3*|Z|**(3/2)*COS(3*PHASE(Z)/2) )*	
CCCC            SIN( 2/3*|Z|**(3/2)*SIN(3*PHASE(Z)/2) )			
CCCC DL1   :  LOGARITHM OF THE OVERFLOW NUMBER				
CCCC DL2   :  - LOGARITHM OF THE UNDERFLOW NUMBER				
CCCC DRE   :  EXP(- 2/3*|Z|**(3/2)*COS(3*PHASE(Z)/2) )*	
CCCC            COS( 2/3*|Z|**(3/2)*SIN(3*PHASE(Z)/2) )			
CCCC F23   :  2/3				
CCCC F23R  :  2/3*|Z|**(3/2)				
CCCC H     :  INTEGRATION STEP				
CCCC IERRO :  ERROR FLAG FOR THE COMPUTATION OF AI(Z) OR BI(Z)				
CCCC IERROH:  ERROR FLAG FOR THE COMPUTATION OF HI(Z)				
CCCC IEXPF :  OVERFLOW PARAMETER				
CCCC IFAC  :  PARAMETER FOR THE COMPUTATION OF THE HOMOGENEOUS AIRY				
CCCC IFACH :	
CCCC           * IF IFACH=1, COMPUTATION OF UNSCALED HI(Z).
CCCC           * IF IFACH=2, COMPUTATION OF SCALED HI(Z). 
CCCC IFACHS:  STORES THE VALUE OF IFACH USED AS INPUT 			
CCCC IFUN  :  PARAMETER FOR THE COMPUTATION OF THE HOMOGENEOUS AIRY
CCCC           FUNCTIONS:
CCCC           * IF IFUN=1, COMPUTATION OF AI(Z) OR BI(Z).
CCCC           * IF IFUN=2, COMPUTATION OF AI'(Z) OR BI'(Z).				
CCCC IMH   :  IMAGINARY PART OF THE FUNCTION HI(Z)				
CCCC IMH1  :  CONTRIBUTION TO THE IMAGINARY PART OF HI(Z)				
CCCC IMH2  :  IMAGINARY PART OF  EXP(2*PI*I/3)*HI(2*PI*I/3)				
CCCC IMH3  :  IMAGINARY PART OF EXP(-2/3*Z**(3/2))*EXP(2*PI*I/3)*
CCCC                             HI(Z*EXP(2*PI*I/3))		                           		
CCCC IMHP  :  IMAGINARY PART OF THE FUNCTION HI'(Z)				
CCCC IMHP1 :  CONTRIBUTION TO THE IMAGINARY PART OF HI'(Z)	                      	
CCCC IMHP2 :  IMAGINARY PART OF  EXP(2*PI*I/3)*HI'(2*PI*I/3)				
CCCC IMHP3 :  IMAGINARY PART OF EXP(-2/3*Z**(3/2))*EXP(2*PI*I/3)*
CCCC                             HI'(Z*EXP(2*PI*I/3))
CCCC IZ    :  FLAG FOR THE PARTICULAR CASE Z=0 (IZ=1) 	                          							
CCCC OVER  :  OVERFLOW NUMBER				
CCCC PH    :  PHASE OF Z				
CCCC PHASE :  FUNCTION FOR THE CALCULATION OF THE PHASE OF 
CCCC          A COMPLEX NUMBER				
CCCC PI    :  3.1415...				
CCCC PI13  :  PI/3				
CCCC PI23  :  PI*2/3				
CCCC PI56  :  PI*5/6				
CCCC PI6   :  PI/6				
CCCC PIHAL :  PI/2				
CCCC R     :  MODULUS OF Z				
CCCC R32   :  |Z|**(3/2)				
CCCC REH   :  REAL PART OF THE FUNCTION HI(Z)				
CCCC REH1  :  CONTRIBUTION TO THE REAL PART OF HI(Z)				
CCCC REH2  :  REAL PART OF EXP(2*PI*I/3)*HI(2*PI*I/3)				
CCCC REH3  :  REAL PART OF EXP(-2/3*Z**(3/2))*EXP(2*PI*I/3)*
CCCC                        HI(Z*EXP(2*PI*I/3))				
CCCC REHP  :  REAL PART OF THE FUNCTION HI'(Z)				
CCCC REHP1 :  CONTRIBUTION TO THE REAL PART OF HI'(Z)				
CCCC REHP2 :  REAL PART OF EXP(2*PI*I/3)*HI'(2*PI*I/3)				
CCCC REHP3 :  REAL PART OF EXP(-2/3*Z**(3/2))*EXP(2*PI*I/3)*
CCCC                        HI'(Z*EXP(2*PI*I/3))				
CCCC S     :  SIN(+-2*PI/3)				
CCCC S1    :  SIN(3*PHASE(Z)/2)				
CCCC S11   :  SIN( 2/3*|Z|**(3/2)*SIN(3*PHASE(Z)/2) )								
CCCC SQRT3 :  3**(1/2)				
CCCC SS    :  -SIN(PI/6)				
CCCC TH15  :  PHASE(Z)*3/2				
CCCC U     :  REAL PART OF Z*EXP(+-2*PI/3)				
CCCC UNDER :  UNDERFLOW NUMBER				
CCCC V     :  IMAGINARY PART OF Z*EXP(+-2*PI/3)				
CCCC X     :  REAL PART OF Z				
CCCC Y     :  IMAGINARY PART OF Z				
CCCC YA    :  IMAGINARY PART OF Z				
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     AUTHORS:                                               
C        AMPARO GIL    (U. AUTONOMA DE MADRID, MADRID, SPAIN). 
C                      E-MAIL: AMPARO.GIL@UAM.ES
C        JAVIER SEGURA (U. CARLOS III DE MADRID, MADRID, SPAIN).
C                      E-MAIL: JSEGURA@MATH.UC3M.ES
C        NICO M. TEMME (CWI, AMSTERDAM, THE NETHERLANDS).
C                      E-MAIL: NICO.TEMME@CWI.NL
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C    REFERENCES:                                                
C         ON NON-OSCILLATING INTEGRALS FOR COMPUTING 
C         INHOMOGENEOUS AIRY FUNCTIONS.
C         A. GIL, J. SEGURA, N.M. TEMME
C         MATH. COMPUT. 70 (2001) 1183-1194.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      DOUBLE PRECISION X,Y,REH,IMH,REHP,IMHP
      DOUBLE PRECISION YA,F23,PI,PIHAL,PI13,PI23,PI6,PI56,SQRT3,R,H,
     *PHASE,PH,C,S,U,V,D1MACH,OVER,UNDER,DL1,DL2,COVER,R32
      DOUBLE PRECISION TH15,S1,C1,F23R,DF1,DF2,S11,C11,DEX,DRE,DIMA
      DOUBLE PRECISION AIR1,AII1,AIR,AII,REH1,IMH1,REHP1,IMHP1
      DOUBLE PRECISION REHP2,IMHP2,REH2,IMH2,SS,CC
      DOUBLE PRECISION REHP3,IMHP3,REH3,IMH3
      INTEGER N,IFUN,IFAC,IERRO,IFACH,IFACHS,IERROH,IEXPF,IZ
      COMMON/PARAM1/PI,PIHAL
      YA=Y
      IFACHS=IFACH
      IF (YA.LT.0.D0) THEN
        Y=-Y
      ENDIF
      PI=ACOS(-1.D0)         
      PIHAL=PI*0.5D0
      PI13=PI/3.D0
      PI23=2.D0*PI13
      F23=.6666666666666667D0
      SQRT3=1.7320508075688772935D0
      R=SQRT(X*X+Y*Y)
      R32=R*SQRT(R)
      PH=PHASE(X,Y) 
      H=0.1D0 
      IERROH=0
      IFAC=1
      IEXPF=0
      COVER=F23*R32*ABS(COS(1.5D0*PH))
CCC MACHINE DEPENDENT CONSTANT (OVERFLOW NUMBER) 
      OVER=D1MACH(2)*1.D-3
CCC MACHINE DEPENDENT CONSTANT (UNDERFLOW NUMBER)  
      UNDER=D1MACH(1)*1.D+3 
      DL1=LOG(OVER)
      DL2=-LOG(UNDER)
      IF (DL1.GT.DL2) OVER=1/UNDER      
      IF (IFACH.EQ.2) THEN
CCC IS Z INSIDE THE SECTOR FOR NORMALIZED FUNCTIONS?
CCC   IF NOT, NON NORMALIZED FUNCTIONS ARE COMPUTED 
        IF (PH.GT.PI13) THEN
          IFACH=1
        ENDIF
      ENDIF
      IF (IFACH.EQ.1) THEN                 
        IF (PH.LE.PI13) THEN                                 
          IF (COVER.GE.LOG(OVER)) THEN
CCC OVERFLOW/UNDERFLOW PROBLEMS. 
CCC   CALCULATION ABORTED
            IERROH=1
            REH=0
            IMH=0
            REHP=0
            IMHP=0
          ENDIF
        ENDIF
      ELSE
        IF (COVER.GE.(LOG(OVER)*0.2)) THEN
          IEXPF=1
          IFAC=2     
        ENDIF
      ENDIF
      IZ=0
CCC  PARTICULAR CASE: Z=0
      IF ((ABS(X).LE.UNDER).AND.(ABS(Y).LE.UNDER)) THEN
        REH=.4099510849640005D0
        IMH=0.D0
        REHP=.298858904902551D0
        IMHP=0.D0
        IZ=1
      ENDIF
      IF ((IERROH.EQ.0).AND.(IZ.EQ.0)) THEN   
        IF (R.LT.1.5) THEN
CCC USE OF SERIES
          CALL HIZSER(X,Y,REH1,IMH1,REHP1,IMHP1)
          IF (IFACH.EQ.2) THEN
            TH15=1.5D0*PH
            S1=SIN(TH15)
            C1=COS(TH15)
            F23R=F23*R32
            DF1=F23R*C1
            DF2=F23R*S1
            S11=SIN(DF2)
            C11=COS(DF2)
            DEX=EXP(-DF1)
            DRE=DEX*C11
            DIMA=-DEX*S11
            REH=(DRE*REH1-DIMA*IMH1)/PI
            IMH=(DRE*IMH1+DIMA*REH1)/PI
            REHP=(DRE*REHP1-DIMA*IMHP1)/PI
            IMHP=(DRE*IMHP1+DIMA*REHP1)/PI
          ELSE
            REH=REH1/PI
            IMH=IMH1/PI
            REHP=REHP1/PI
            IMHP=IMHP1/PI
          ENDIF
        ELSE
          IF ((R.GT.20).AND.(PH.GT.PI23)) THEN          
CCC USE OF ASYMPTOTIC EXPANSION                            
            CALL HIZEXP(X,Y,REH,IMH,REHP,IMHP)                
            REH=-REH/PI             
            IMH=-IMH/PI
            REHP=REHP/PI
            IMHP=IMHP/PI
          ELSE
            IF (PH.LT.PI23) THEN
CCCCCCCCC CONNECTION FORMULAE
CCC     N= 1: TRANSFORM Z TO W= U+IV=Z EXP( 2 PI I/3)
              IF (IEXPF.EQ.0) THEN
                N=1
                C=-0.5D0
                S=N*0.5*SQRT3
                U=X*C-Y*S
                V=X*S+Y*C
                IF (R.GT.20) THEN
                  CALL HIZEXP(U,V,REH1,IMH1,REHP1,IMHP1)                
                  REH1=-REH1/PI             
                  IMH1=-IMH1/PI
                  REHP1=REHP1/PI
                  IMHP1=IMHP1/PI
                ELSE 
                  CALL HIZINT(U,V,H,REH1,IMH1,REHP1,IMHP1)
                ENDIF
                REH2=C*REH1-S*IMH1
                IMH2=S*REH1+C*IMH1                          
                S=-S                           
                REHP2=C*REHP1-S*IMHP1                    
                IMHP2=S*REHP1+C*IMHP1
                IF (IFACH.EQ.2) THEN
                  TH15=1.5D0*PH
                  S1=SIN(TH15)
                  C1=COS(TH15)
                  F23R=F23*R32
                  DF1=F23R*C1
                  DF2=F23R*S1
                  S11=SIN(DF2)
                  C11=COS(DF2)
                  DEX=EXP(-DF1)
                  DRE=DEX*C11
                  DIMA=-DEX*S11
                  REH3=(DRE*REH2-DIMA*IMH2)
                  IMH3=(DRE*IMH2+DIMA*REH2)
                  REHP3=(DRE*REHP2-DIMA*IMHP2)
                  IMHP3=(DRE*IMHP2+DIMA*REHP2)
                  IFAC=2
                ELSE
                  REH3=REH2
                  IMH3=IMH2
                  REHP3=REHP2
                  IMHP3=IMHP2
                ENDIF
              ELSE
                REH3=0
                IMH3=0
                REHP3=0
                IMHP3=0 
                IFAC=2
              ENDIF      
CCC     N= -1: TRANSFORM Z TO W= U+IV=Z EXP( -2 PI I/3)                   
              N=-1                            
              C=-0.5D0                          
              S=N*0.5*SQRT3
              U=X*C-Y*S
              V=X*S+Y*C
              IFUN=1                                 
              CALL AIZ(IFUN,IFAC,U,V,AIR1,AII1,IERRO)
              PI6=PI/6.D0
              CC=COS(PI6)
              SS=-SIN(PI6)
              AIR=CC*AIR1-SS*AII1
              AII=SS*AIR1+CC*AII1
              REH=REH3+2.D0*AIR
              IMH=IMH3+2.D0*AII
              IFUN=2
              CALL AIZ(IFUN,IFAC,U,V,AIR1,AII1,IERRO)
              PI56=5.D0*PI6
              CC=COS(PI56)
              SS=-SIN(PI56)
              AIR=CC*AIR1-SS*AII1
              AII=SS*AIR1+CC*AII1
              REHP=REHP3+2.D0*AIR
              IMHP=IMHP3+2.D0*AII
            ELSE
CCCCCCCCC NUMERICAL QUADRATURE (TRAPEZOIDAL)
              CALL HIZINT(X,Y,H,REH,IMH,REHP,IMHP)
            ENDIF
          ENDIF
        ENDIF
      ENDIF
      IF (YA.LT.0.D0) THEN
        Y=YA
        IMH=-IMH
        IMHP=-IMHP
      ENDIF
      IF (YA.EQ.0.D0) THEN
        IMH=0.D0
        IMHP=0.D0
      ENDIF      
      IFACH=IFACHS
      RETURN     
      END

      SUBROUTINE HIZINT(X,Y,H,REH,IMH,REHP,IMHP)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC   CALCULATION OF HI(Z) BY NUMERICAL QUADRATURE      CCC
CCC
CCC   INPUTS:
CCC          X,Y,  REAL AND IMAGINARY PARTS OF Z
CCC   OUTPUTS:
CCC          REH,IMH,    REAL AND IMAGINARY PARTS OF HI(Z)
CCC          REHP,IMHP,  REAL AND IMAGINARY PARTS OF HI'(Z) 
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 
CCCC                  LIST OF VARIABLES 
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCCC  A     : LOWER INTEGRATION LIMIT
CCCC  ABB   : |RE(CONTRIBUTION TO HI(Z))|+
CCCC          |IM(CONTRIBUTION TO HI(Z))|+
CCCC          |RE(CONTRIBUTION TO HI'(Z))|+
CCCC          |IM(CONTRIBUTION TO HI'(Z))|
CCCC  B     : UPPER INTEGRATION LIMIT
CCCC  D1MACH: COMPUTES THE MACHINE DEPENDENT CONSTANTS
CCCC  EPS   : PRECISION
CCCC  H     : INTEGRATION STEP
CCCC  HS    : INTEGRATION STEP/PI
CCCC  IMG   : CONTRIBUTION TO THE IMAGINARY PART OF HI(Z) 
CCCC  IMGP  : CONTRIBUTION TO THE IMAGINARY PART OF HI'(Z)  
CCCC  PI    : 3.1415...
CCCC  REG   : CONTRIBUTION TO THE REAL PART OF HI(Z)
CCCC  REGP  : CONTRIBUTION TO THE REAL PART OF HI'(Z)
CCCC  RR    : PARAMETER USED IN THE ROUTINE INTU TO DECIDE
CCCC          IF THE STEEPEST DESCENT CONTOUR IS USED OR 
CCCC          THE MODIFIED CONTOUR 
CCCC  SQRT3 : 3**(1/2)
CCCC  T     : INTEGRATION ABSCISSA    
CCCC  YA    : IMAGINARY PART OF Z
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      DOUBLE PRECISION X,Y,H,REH,IMH,REHP,IMHP
      DOUBLE PRECISION YA,EPS,SQRT3,PI,RR,A,B,T,REG,IMG,REGP,IMGP,
     + ABB,HS,D1MACH
      COMMON/CONST/SQRT3,RR
      EPS=D1MACH(3)
      IF (EPS.LT.1.D-16) EPS=1.D-16
CCCCC   CONSTANTS
      SQRT3=SQRT(3.D0)
      PI=ACOS(-1.D0)
      YA=Y
      IF (YA.LT.0.D0) THEN
        Y=-Y
      ENDIF
CCCCC   RR IS USED IN INTU TO DECIDE IF THE STEEPEST DESCENT
CCCCC      CONTOUR IS USED OR THE MODIFIED CONTOUR
      RR=0.8D0
CCCCC   INTEGRATION LIMITS: A,B
      A=-4.38D0
      B=-A
      T=A
      REH=0.D0
      IMH=0.D0
      REHP=0.D0
      IMHP=0.D0
CCCCC   DO WHILE T<B
CCCCC   CALL TO THE INTEGRATION ROUTINE
 10   CALL INTT(T,X,Y,REG,IMG,REGP,IMGP)
      REH=REH+REG
      IMH=IMH+IMG
      REHP=REHP+REGP
      IMHP=IMHP+IMGP
      ABB=ABS(REG)+ABS(IMG)+ABS(REGP)+ABS(IMGP)
      IF ((ABB.LT.EPS).AND.(T.GT.0)) B=T
      T=T+H
      IF (T.LT.B) GOTO 10
      HS=H/PI
      REH=HS*REH
      IMH=HS*IMH
      REHP=HS*REHP
      IMHP=HS*IMHP
      IF (YA.LT.0.D0) THEN
        Y=YA
        IMH=-IMH
        IMHP=-IMHP
      ENDIF
      RETURN
      END

      SUBROUTINE INTT(T,X,Y,RE,IM,REP,IMP)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC   ELEMENTARY INTEGRATION ROUTINE             C
CCC   INPUTS:                                    C
CCC      T,    INTEGRATION ABSCISSA              C
CCC      X,Y,  REAL AND IMAGINARY PARTS OF Z     C
CCC   OUTPUTS:                                   C 
CCC      RE,IM,   REAL AND IMAGINARY PARTS OF    C
CCC               THE ELEMENTARY CONTRIBUTIONS   C
CCC               TO HI(Z).                      C
CCC      REP,IMP, REAL AND IMAGINARY PARTS OF    C
CCC               THE ELEMENTARY CONTRIBUTIONS   C
CCC               TO HI'(Z).                     C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC         THIS ROUTINE CALLS INTU              C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC         LIST OF VARIABLES
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC  CHT   : HYPERBOLIC COSINUS OF THE INTEGRATION 
CCC          ABCISSA (T)
CCC  DUDT  : U'(T)
CCC  EXPSHT: EXP(HYPERBOLIC SINUS OF T)
CCC  SHT   : HYPERBOLIC SINUS OF THE INTEGRATION 
CCC          ABCISSA (T)     
CCC  U     : LOG(1+EXP(HYPERBOLIC SINUS OF T))
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      DOUBLE PRECISION T,X,Y,RE,IM,REP,IMP
      DOUBLE PRECISION DUDT,SHT,CHT,EXPSHT,U
      SHT=SINH(T)
      CHT=COSH(T)
      EXPSHT=EXP(SHT)   
      DUDT=CHT*EXPSHT/(1.D0+EXPSHT)
      U=LOG(1.D0+EXPSHT)
      CALL INTU(U,X,Y,RE,IM,REP,IMP)
      RE=RE*DUDT
      IM=IM*DUDT
      REP=REP*DUDT
      IMP=IMP*DUDT
      RETURN
      END    
      
      SUBROUTINE INTU(U,X,Y,RE,IM,REP,IMP)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC   PART OF THE ELEMENTARY INTEGRATION ROUTINE    C
CCC   INPUTS:                                       C
CCC      U,    INTEGRATION ABSCISSA                 C
CCC      X,Y,  REAL AND IMAGINARY PARTS OF Z        C
CCC   OUTPUTS:                                      C 
CCC      RE,IM,   REAL AND IMAGINARY PARTS OF       C
CCC               THE ELEMENTARY CONTRIBUTIONS      C
CCC               TO HI(Z).                         C
CCC      REP,IMP, REAL AND IMAGINARY PARTS OF       C
CCC               THE ELEMENTARY CONTRIBUTIONS      C
CCC               TO HI'(Z).                        C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC         LIST OF VARIABLES
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC  
CCC  DVDU  : V'(U)
CCC  FII   : V*(U**2-V**2/3.D0-X)-Y*U
CCC  FIR   : U*(U**2/3.D0-V**2-X)+Y*V
CCC  FIREXP: EXP(-(U*(U**2/3.D0-V**2-X)+Y*V))
CCC  P     : U**2+1, (U**2+1)**2, 
CCC          -SIN(V*(U**2-V**2/3.D0-X)-Y*U)
CCC  Q     : (U**2-X)**(1/2) OR 
CCC          COS(V*(U**2-V**2/3.D0-X)-Y*U)
CCC  RR    : PARAMETER USED TO DECIDE IF THE STEEPEST 
CCC          DESCENT CONTOUR IS USED OR 
CCCC         THE MODIFIED CONTOUR 
CCC  SQRT3 : 3**(1/2)
CCC  TAU   : -Y/X
CCC  TETA  : ARCSIN(3.D0/2.D0*Y*U/(U**2-X)**(3/2))
CCC  U2    : U*U
CCC  V     : 2.D0*(U**2-X)**(1/2)*
CCC          SIN(1/3*ARCSIN(3.D0/2.D0*Y*U/(U**2-X)**(3/2)))
CCC  V2    : V*V
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      DOUBLE PRECISION U,X,Y,RE,IM,REP,IMP
      DOUBLE PRECISION U2,V,V2,DVDU,TETA,P,Q,TAU,FIR,
     +FII,FIREXP,SQRT3,RR
      COMMON/CONST/SQRT3,RR
      U2=U*U
      IF (Y.EQ.0) THEN
        FIREXP=EXP(-U*(U2/3-X))
        RE=FIREXP
        IM=0.D0
        REP=U*RE
        IMP=0.D0
      ELSE
        IF (Y.LT.(-RR*X*SQRT3)) THEN
          P=3.D0/2.D0*Y*U
          Q=SQRT(U2-X)
          TETA=ASIN(P/(Q*Q*Q))
          V=2.D0*Q*SIN(TETA/3.D0)
          V2=V*V
          DVDU=(2*U*V-Y)/(V2-U2+X)
          FIR=U*(U2/3.D0-V2-X)+Y*V
          FIREXP=EXP(-FIR)
          RE=FIREXP
          IM=FIREXP*DVDU
          REP=U*RE-V*IM
          IMP=U*IM+V*RE
        ELSE
          TAU=-Y/X
          V=TAU*U/(U2+1.D0)
          P=U2+1.D0
          P=P*P
          DVDU=-TAU*(U2-1.D0)/P
          V2=V*V
          FIR=U*(U2/3.D0-V2-X)+Y*V
          FII=V*(U2-V2/3.D0-X)-Y*U
          FIREXP=EXP(-FIR)
          P=-SIN(FII)
          Q=COS(FII)
          RE=FIREXP*(Q-P*DVDU)
          IM=FIREXP*(P+Q*DVDU)
          REP=U*RE-V*IM
          IMP=U*IM+V*RE
        ENDIF
      ENDIF
      RETURN
      END
  
      SUBROUTINE HIZSER(X,Y,REH,IMH,REHP,IMHP)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC   HI(Z), HI'(Z)  SERIES, COMPLEX Z     CCC
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC         LIST OF VARIABLES
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC CK    : SUCCESIVE COEFFICIENTS IN THE SERIES 
CCC CO    : ARRAY OF THE COEFFICIENTS OF THE 
CCC         SERIES
CCC D1MACH: FUNCTION FOR THE COMPUTATION OF
CCC         MACHINE DEPENDENT CONSTANTS
CCC DELTAI: CONTRIBUTION TO THE IMAGINARY PART OF
CCC         THE SERIES FOR HI(Z)
CCC DELTAR: CONTRIBUTION TO THE REAL PART OF THE
CCC         SERIES FOR HI(Z)
CCC DELTIP: CONTRIBUTION TO THE IMAGINARY PART OF 
CCC         THE SERIES FOR HI'(Z)
CCC DELTRP: CONTRIBUTION TO THE REAL PART OF THE
CCC         SERIES FOR HI'(Z)
CCC DFI   : ELEMENTARY CONTRIBUTION TO THE 
CCC         IMAGINARY PART OF THE SERIES FOR HI(Z)
CCC         AND HI'(Z)
CCC DFII  : ELEMENTARY CONTRIBUTION TO THE 
CCC         IMAGINARY PART OF THE SERIES FOR HI(Z)
CCC         AND HI'(Z)
CCC DFR   : ELEMENTARY CONTRIBUTION TO THE REAL
CCC         PART OF THE SERIES FOR HI(Z) AND HI'(Z)
CCC DFRR  : ELEMENTARY CONTRIBUTION TO THE REAL
CCC         PART OF THE SERIES FOR HI(Z) AND HI'(Z)
CCC EPS   : PRECISION
CCC IMH   : IMAGINARY PART OF HI(Z)
CCC IMHP  : IMAGINARY PART OF HI'(Z)
CCC R2    : |Z|**2 
CCC REH   : REAL PART OF HI(Z)
CCC REHP  : REAL PART OF HI'(Z)
CCC SUMAI : ACCUMULATES THE CONTRIBUTION TO THE IMAGINARY 
CCC         PART OF HI(Z)
CCC SUMAIP: ACCUMULATES THE CONTRIBUTION TO THE IMAGINARY 
CCC         PART OF HI'(Z)
CCC SUMAR : ACCUMULATES THE CONTRIBUTION TO THE REAL 
CCC         PART OF HI(Z)
CCC SUMARP: ACCUMULATES THE CONTRIBUTION TO THE REAL 
CCC         PART OF HI'(Z)
CCC X     : REAL PART OF Z
CCC Y     : IMAGINARY PART OF Z
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      DOUBLE PRECISION X,Y,EPS,D1MACH,REH,IMH,REHP,IMHP
      DOUBLE PRECISION R2,CK,DFRR,DFII,SUMAR,SUMAI,DFR,DFI,DELTAR,
     *DELTAI,SUMARP,SUMAIP,DELTRP,DELTIP
      DOUBLE PRECISION CO(0:40)
      INTEGER K
      SAVE CO 
      DATA CO(0)/1.2878993168540690872D0/
      DATA CO(1)/.93889294010174456629D0/
      DATA CO(2)/.50000000000000000000D0/
      DATA CO(3)/.21464988614234484787D0/
      DATA CO(4)/.78241078341812047192D-1/
      DATA CO(5)/.25000000000000000000D-1/
      DATA CO(6)/.71549962047448282623D-2/
      DATA CO(7)/.18628828176621915998D-2/
      DATA CO(8)/.44642857142857142857D-3/
      DATA CO(9)/.99374947288122614757D-4/
      DATA CO(10)/.20698697974024351109D-4/
      DATA CO(11)/.40584415584415584416D-5/
      DATA CO(12)/.75284050975850465722D-6/
      DATA CO(13)/.13268396137195096865D-6/
      DATA CO(14)/.22299129441986584844D-7/
      DATA CO(15)/.35849548083738317012D-8/
      DATA CO(16)/.55284983904979570268D-9/
      DATA CO(17)/.81982093536715385455D-10/
      DATA CO(18)/.11715538589456966343D-10/
      DATA CO(19)/.16165199972216248617D-11/
      DATA CO(20)/.21574235141240890909D-12/
      DATA CO(21)/.27894139498707062723D-13/
      DATA CO(22)/.34989610329472399605D-14/
      DATA CO(23)/.42636828342373302192D-15/
      DATA CO(24)/.50532861410701200586D-16/
      DATA CO(25)/.58316017215787332673D-17/
      DATA CO(26)/.65595120526728157219D-18/
      DATA CO(27)/.71984133063676923909D-19/
      DATA CO(28)/.77137588909771604065D-20/
      DATA CO(29)/.80782168136364725639D-21/
      DATA CO(30)/.82740382831812556219D-22/
      DATA CO(31)/.82943643989001724800D-23/
      DATA CO(32)/.81433637234238634716D-24/
      DATA CO(33)/.78352635257398253995D-25/
      DATA CO(34)/.73924816389484603210D-26/
      DATA CO(35)/.68431627927931625812D-27/
      DATA CO(36)/.62184631156665280952D-28/
      DATA CO(37)/.55499111403516969377D-29/
      DATA CO(38)/.48671143618728041118D-30/
      DATA CO(39)/.41959940051730958805D-31/
      DATA CO(40)/.35576353463792929088D-32/
      EPS=D1MACH(3)
      IF (EPS.LT.1.D-15) EPS=1.D-15
      R2=X*X+Y*Y
      K=0
      CK=CO(0)
      DFRR=1.D0
      DFII=0.D0
      SUMAR=CK
      SUMAI=0.D0
      SUMARP=0.D0
      SUMAIP=0.D0
30    CK=CO(K+1)
      DFR=DFRR
      DFI=DFII  
      DFRR=DFR*X-DFI*Y
      DFII=DFR*Y+DFI*X
      DELTAR=DFRR*CK
      DELTAI=DFII*CK
      DELTRP=DELTAR*(K+1)
      DELTIP=DELTAI*(K+1)
      SUMAR=SUMAR+DELTAR
      SUMAI=SUMAI+DELTAI
      SUMARP=SUMARP+DELTRP
      SUMAIP=SUMAIP+DELTIP
      K=K+1
      IF (SUMAR.NE.0) THEN
        IF (ABS(DELTAR/SUMAR).GT.EPS)  GOTO 30
      ENDIF
      IF (SUMAI.NE.0) THEN
        IF (ABS(DELTAI/SUMAI).GT.EPS) GOTO 30
      ENDIF 
      IF (SUMARP.NE.0) THEN
        IF (ABS(DELTRP/SUMARP).GT.EPS)  GOTO 30
      ENDIF
      IF (SUMAIP.NE.0) THEN
        IF (ABS(DELTIP/SUMAIP).GT.EPS) GOTO 30
      ENDIF 
      REH=SUMAR
      IMH=SUMAI
      REHP=(X*SUMARP+Y*SUMAIP)/R2
      IMHP=-(Y*SUMARP-X*SUMAIP)/R2
      RETURN
      END

      SUBROUTINE HIZEXP(X,Y,REH,IMH,REHP,IMHP)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC   HI(Z), HI'(Z) EXPANSION, COMPLEX Z CCC
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC         LIST OF VARIABLES
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC C1    : COS(3*PHASE(Z))
CCC C2    : COS(PHASE(Z))
CCC CK    : SUCCESIVE COEFFICIENTS OF THE EXPANSION
CCC CO    : ARRAY OF THE COEFFICIENTS OF THE 
CCC         EXPANSION
CCC D1MACH: MACHINE DEPENDENT CONSTANTS
CCC DELTAI: CONTRIBUTION TO THE IMAGINARY PART OF
CCC         THE EXPANSION FOR HI(Z)
CCC DELTAR: CONTRIBUTION TO THE REAL PART OF THE
CCC         EXPANSION FOR HI(Z)
CCC DELTIP: CONTRIBUTION TO THE IMAGINARY PART OF 
CCC         THE EXPANSION FOR HI'(Z)
CCC DELTRP: CONTRIBUTION TO THE REAL PART OF THE
CCC         EXPANSION FOR HI'(Z)
CCC DFII  : ELEMENTARY CONTRIBUTION TO THE 
CCC         IMAGINARY PART OF THE EXPANSION FOR HI(Z)
CCC         AND HI'(Z)
CCC DFRR  : ELEMENTARY CONTRIBUTION TO THE REAL
CCC         PART OF THE SERIES FOR HI(Z) AND HI'(Z)
CCC EPS   : PRECISION
CCC IMH   : IMAGINARY PART OF HI(Z)
CCC IMH1  : CONTRIBUTION TO THE REAL AND IMAGINARY PARTS
CCC         OF THE SERIES FOR HI(Z) 
CCC IMHP  : IMAGINARY PART OF HI'(Z)
CCC IMHP1 : CONTRIBUTION TO THE REAL AND IMAGINARY PARTS
CCC         OF THE SERIES FOR HI'(Z) 
CCC PH    : PHASE(Z)
CCC PH3   : 3*PHASE(Z)
CCC PHASE : FUNCTION FOR THE CALCULATION OF THE PHASE OF 
CCC         A COMPLEX NUMBER
CCC R     : |Z|
CCC R2M   : 1/|Z|**2
CCC R3M   : 1/|Z|**3
CCC R3MK  : AUXILIARY VARIABLE
CCC REH   : REAL PART OF HI(Z)
CCC REH1  : CONTRIBUTION TO THE REAL AND IMAGINARY PARTS
CCC         OF THE SERIES FOR HI(Z) 
CCC REHP  : REAL PART OF HI'(Z)
CCC REHP1 : CONTRIBUTION TO THE REAL AND IMAGINARY PARTS
CCC         OF THE SERIES FOR HI'(Z) 
CCC RM    : 1/|Z|
CCC S1    : SIN(3*PHASE(Z))
CCC S2    : SIN(PHASE(Z))
CCC SUMAI : ACCUMULATES THE CONTRIBUTION TO THE IMAGINARY 
CCC         PART OF HI(Z)
CCC SUMAIP:  ACCUMULATES THE CONTRIBUTION TO THE IMAGINARY 
CCC         PART OF HI'(Z)
CCC SUMAR : ACCUMULATES THE CONTRIBUTION TO THE REAL 
CCC         PART OF HI(Z)
CCC SUMARP: ACCUMULATES THE CONTRIBUTION TO THE REAL 
CCC         PART OF HI'(Z)
CCC X     : REAL PART OF Z
CCC Y     : IMAGINARY PART OF Z
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      DOUBLE PRECISION X,Y,EPS,D1MACH,REH,IMH,REHP,IMHP 
      DOUBLE PRECISION CK,DFRR,DFII,SUMAR,SUMAI,DELTAR,DELTAI
      DOUBLE PRECISION PHASE,R,RM,R3M,R3MK,PH,PH3
      DOUBLE PRECISION R2M,CKK,SUMARP,SUMAIP,DELTRP,DELTIP,
     *  REHP1,IMHP1
      DOUBLE PRECISION REH1,IMH1
      DOUBLE PRECISION C1,S1,C2,S2
      DOUBLE PRECISION CO(0:40)
      INTEGER K,KK1
      SAVE CO 
      DATA CO(0)/2.D0/
      DATA CO(1)/40.D0/
      DATA CO(2)/2240.D0/
      DATA CO(3)/246400.D0/
      DATA CO(4)/44844800.D0/
      DATA CO(5)/12197785600.D0/
      DATA CO(6)/4635158528000.D0/
      DATA CO(7)/2345390215168000.D0/
      DATA CO(8)/1524503639859200000.D0/
      DATA CO(9)/.12378969555656704000D22/
      DATA CO(10)/.12279937799211450368D25/
      DATA CO(11)/.14613125981061625938D28/
      DATA CO(12)/.20546055129372646069D31/
      DATA CO(13)/.33695530412171139553D34/
      DATA CO(14)/.63751943539827796034D37/
      DATA CO(15)/.13783170193310769502D41/
      DATA CO(16)/.33768766973611385281D44/
      DATA CO(17)/.93066721779272977835D47/
      DATA CO(18)/.28664550308016077173D51/
      DATA CO(19)/.98090091154031016086D54/
      DATA CO(20)/.37097672474454530284D58/
      DATA CO(21)/.15432631749373084598D62/
      DATA CO(22)/.70311070250143773429D65/
      DATA CO(23)/.34944601914321455394D69/
      DATA CO(24)/.18877073954116450204D73/
      DATA CO(25)/.11046863677948946659D77/
      DATA CO(26)/.69816178444637342887D80/
      DATA CO(27)/.47516891049420175569D84/
      DATA CO(28)/.34734847357126148341D88/
      DATA CO(29)/.27204332450101199381D92/
      DATA CO(30)/.22775467127224724121D96/
      DATA CO(31)/.20338492144611678640D100/
      DATA CO(32)/.19333770632667861716D104/
      DATA CO(33)/.19527108338994540333D108/
      DATA CO(34)/.20917438452730951604D112/
      DATA CO(35)/.23724558693087445310D116/
      DATA CO(36)/.28445745873011846926D120/
      DATA CO(37)/.36000935976883793470D124/
      DATA CO(38)/.48025248593162980489D128/
      DATA CO(39)/.67437054074519457203D132/
      DATA CO(40)/.99550579224805622723D136/
      EPS=D1MACH(3)
      IF (EPS.LT.1.D-15) EPS=1.D-15  
      R=SQRT(X*X+Y*Y)
      RM=1.D0/R
      R2M=RM*RM
      R3M=R2M*RM
      PH=PHASE(X,Y)
      PH3=3.D0*PH
      K=0
      CK=CO(0)
      SUMAR=CK
      SUMAI=0.D0
      SUMARP=CK*4.D0
      SUMAIP=0.D0
      R3MK=1.D0
31    KK1=K+1
      CK=CO(KK1)
      R3MK=R3MK*R3M
      DFRR=COS(PH3*KK1)
      DFII=-SIN(PH3*KK1)
      DELTAR=DFRR*CK*R3MK
      DELTAI=DFII*CK*R3MK
      CKK=3.D0*KK1+4.D0
      DELTRP=DFRR*CK*R3MK*CKK
      DELTIP=DFII*CK*R3MK*CKK
      SUMAR=SUMAR+DELTAR
      SUMAI=SUMAI+DELTAI
      SUMARP=SUMARP+DELTRP
      SUMAIP=SUMAIP+DELTIP
      K=K+1
      IF (SUMAR.NE.0) THEN
        IF (ABS(DELTAR/SUMAR).GT.EPS) GOTO 31
      ENDIF                                      
      IF (SUMAI.NE.0) THEN                      
        IF (ABS(DELTAI/SUMAI).GT.EPS) GOTO 31
      ENDIF                              
      IF (SUMARP.NE.0) THEN                     
        IF (ABS(DELTRP/SUMARP).GT.EPS) GOTO 31
      ENDIF
      IF (SUMAIP.NE.0) THEN
        IF (ABS(DELTIP/SUMAIP).GT.EPS) GOTO 31
      ENDIF
      C1=COS(PH3)
      S1=-SIN(PH3)
      REH1=1.D0+R3M*(SUMAR*C1-SUMAI*S1)
      IMH1=R3M*(SUMAR*S1+SUMAI*C1)
      REHP1=1.D0+R3M*(SUMARP*C1-SUMAIP*S1)
      IMHP1=R3M*(SUMARP*S1+SUMAIP*C1)
      C2=COS(PH)
      S2=-SIN(PH)
      REH=RM*(REH1*C2-IMH1*S2)
      IMH=RM*(REH1*S2+IMH1*C2)
      C2=COS(2.D0*PH)
      S2=-SIN(2.D0*PH)
      REHP=R2M*(REHP1*C2-IMHP1*S2)
      IMHP=R2M*(REHP1*S2+IMHP1*C2)
      RETURN
      END
   
